pkgname=dropbear
pkgver=2022.83
pkgrel=1
pkgdesc="Lightweight replacement for sshd"
url="http://matt.ucc.asn.au/dropbear/dropbear.html"
license=('MIT')
source=(https://matt.ucc.asn.au/$pkgname/releases/$pkgname-$pkgver.tar.bz2
	dropbear.init
	dropbear.default)
sha256sums=('bc5a121ffbc94b5171ad5ebe01be42746d50aa797c9549a4639894a16749443b'
            SKIP SKIP)

build() {
  cd $pkgname-$pkgver

  ./configure --prefix=/usr
  make
}

package() {
  cd $pkgname-$pkgver

  make install DESTDIR="${pkgdir}/"

  # Configuration files
  install -d "${pkgdir}/etc/$pkgname"
  
  install -D -m755 ../dropbear.init "${pkgdir}/etc/init.d/dropbear"
  install -D -m644 ../dropbear.default "${pkgdir}/etc/default/dropbear"

}

